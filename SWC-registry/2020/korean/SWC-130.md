# 알림: 이 콘텐츠는 더 이상 적극적으로 유지되지 않습니다.

SWC 레지스트리의 내용은 2020년 이후 철저하게 업데이트되지 않았습니다. 불완전하고 오류가 포함되어 있으며 중요한 누락이 있을 수 있습니다.

보안 검토자를 위한 지침으로 작성된 현재 유지되는 스마트 컨트랙트 취약점에 대한 안내는
[EEA EthTrust Security Levels specification](https://entethalliance.org/specs/ethtrust-sl)을 참조하십시오. 최신 릴리스 버전과 함께 사양을 개발하는 그룹의 최신 작업을 나타내는
[Editor's draft](https://entethalliance.github.io/eta-registry/security-levels-spec.html)도 제공됩니다.

현재 유지되고 있는 개발자를 위한 보안 고려사항에 대한 일반적인 지침은
[Smart Contract Security Verification Standard (SCSVS)](https://github.com/ComposableSecurity/SCSVS)를 통해서도 제공됩니다.

# 제목

Right-To-Left-Override 제어 문자 (U+202E)

## 관계

- [CWE-451: User Interface (UI) Misrepresentation of Critical Information](http://cwe.mitre.org/data/definitions/451.html)
- EEA EthTrust Security Levels specification:
  - [**[S] No Unicode Direction Control Characters**](https://entethalliance.org/specs/ethtrust-sl/#req-1-unicode-bdo)
  - [**[M] No Unnecessary Unicode Controls**](https://entethalliance.org/specs/ethtrust-sl/#req-2-unicode-bdo)
  - [**[M] No Homoglyph-style Attack**](https://entethalliance.org/specs/ethtrust-sl/#req-2-no-homoglyph-attack)

## 설명

악의적인 행위자는 Right-To-Left-Override 유니코드 문자를 사용하여 RTL 텍스트 렌더링을 강제하고 컨트랙트의 실제 의도에 대해 사용자를 혼란스럽게 할 수 있습니다.

## 해결방법

U+202E 문자의 정당한 사용 사례는 매우 적습니다. 스마트 컨트랙트의 소스 코드에 나타나서는 안 됩니다.

## 참고자료

- [Outsmarting Smart Contracts](https://youtu.be/P_Mtd5Fc_3E?t=1813)


## 샘플

### guess_the_number.sol

```solidity
/*
 * @source: https://youtu.be/P_Mtd5Fc_3E
 * @author: Shahar Zini
 */
pragma solidity ^0.5.0;

contract GuessTheNumber
{
    uint _secretNumber;
    address payable _owner;
    event success(string);
    event wrongNumber(string);

    constructor(uint secretNumber) payable public
    {
        require(secretNumber <= 10);
        _secretNumber = secretNumber;
        _owner = msg.sender;
    }

    function getValue() view public returns (uint)
    {
        return address(this).balance;
    }

    function guess(uint n) payable public
    {
        require(msg.value == 1 ether);

        uint p = address(this).balance;
        checkAndTransferPrize(/*The prize‮/*rebmun desseug*/n , p/*‭
		        /*The user who should benefit */,msg.sender);
    }

    function checkAndTransferPrize(uint p, uint n, address payable guesser) internal returns(bool)
    {
        if(n == _secretNumber)
        {
            guesser.transfer(p);
            emit success("You guessed the correct number!");
        }
        else
        {
            emit wrongNumber("You've made an incorrect guess!");
        }
    }

    function kill() public
    {
        require(msg.sender == _owner);
        selfdestruct(_owner);
    }
}
```

#### 코멘트

다음 라인:

```
checkAndTransferPrize(/*The prize‮/*rebmun desseug*/n , p/*
```

함수 `guess(uint n)` 내부에서 보이지 않는 방향 제어 문자를 사용하므로 화면에 표시되는 것이 매개변수의 순서를 잘못 표현합니다 - 함수는 `n, p, address` 매개변수로 호출되는데, 이는 문자의 **논리적 순서**이지만 일부는 오른쪽에서 왼쪽으로 표시되므로 'n, p' 세그먼트가 독자에게 역순으로 나타납니다. 보이지 않는 방향 제어 문자가 코드에 포함되어 있기 때문입니다.

텍스트를 문자별로 선택하면 일반적으로 이것을 보여줍니다 - 선택이 갑자기 오른쪽에서 왼쪽 텍스트의 끝으로 점프하고 오른쪽에서 왼쪽으로 확장하기 시작합니다. 콘텐츠에서 유니코드 문자를 명시적으로 확인하는 것도 가능합니다.
